apply plugin: 'com.android.library'

//****************buildSrc依赖选项*****************
apply plugin: 'costtime'
costtime {
    enable true
    costAll false
    timeout 10
    enableLog true
}
//****************打包时配置参数*****************
// 打正式包,正式包开关: bugly关闭，日志关闭，内网地址，严格模式
def isRelease = true
//SDK版本号
def version = "4.3.0.7|20200215"
// 宿主包(jar):true , 热修复包(dex):false
def isPackageJar = true

//********************子项目控制，只有在isReleases=false时才生效*********************
//是否是严格模式,控制短路逻辑的,true会启用短路,false 则不启用短路
def STRICTMODE = false
// 包含热修复功能:true ， 不包含热修复功能:false
def enableHotFix = true
//当前热修复包的版本
def hf_code = 1
def ENABLE_BUGLY = true
def DEBUG_URL = true
//所有的log开关,true 打印所有的日志,false,关闭所有的日志
def logcat = true
// 内部加密字符串试用的密钥，StringFog_G生成时用，
// 在StringFogTransform.groovy之前生成加密串放置于com.analysys.plugin.StringFog bs
def STRING_FOG_KEY = "4.3.0.7|20200201"


//********************日志tag相关*********************
//region 内部日志相关
def tag_hotfix = "analysys_hotfix" //热修复 tag
def tag_snap = "analysys_snap"//安装列表tag
def tag_upload = "analysys_upload"//上传的tag
def tag_oc = "analysys_oc"//oc的tag
def tag_usm = "analysys_usm"//usm的tag
def tag_netinfo = "analysys_netinfo"//网络扫描的tag
def tag_loc = "analysys_loc"//位置信息的tag
def tag_recerver = "analysys_recerver"//广播接收的tag
def tag_cutoff = "analysys_cutoff"//短路的tag
def tag_stringfog = "analysys_stringfog"//字符串混淆的tag
//endregion

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.0'

    defaultConfig {
        def sdkv = "${version}"
        if (enableHotFix) {
            sdkv += "00"
        }
        if (isRelease) {
            ENABLE_BUGLY = false
            DEBUG_URL = true
            logcat = true
            STRICTMODE = true
        }
        buildConfigField "String", "SDK_VERSION", "\"${sdkv}\""
        buildConfigField "String", "STRING_FOG_KEY", "\"${STRING_FOG_KEY}\""
        buildConfigField "boolean", "IS_HOST", "${isPackageJar}"
        buildConfigField "boolean", "enableHotFix", "${enableHotFix}"

        buildConfigField "String", "hf_code", isPackageJar ? "\"0\"" : "\"${hf_code}\""
        buildConfigField "boolean", "ENABLE_BUGLY", "${ENABLE_BUGLY}"
        buildConfigField "boolean", "STRICTMODE", "${STRICTMODE}"
        buildConfigField "boolean", "DEBUG_URL", "${DEBUG_URL}"

        buildConfigField "boolean", "logcat", "${logcat}"
        buildConfigField "String", "tag_hotfix", "\"${tag_hotfix}\""
        buildConfigField "String", "tag_snap", "\"${tag_snap}\""
        buildConfigField "String", "tag_upload", "\"${tag_upload}\""
        buildConfigField "String", "tag_oc", "\"${tag_oc}\""
        buildConfigField "String", "tag_usm", "\"${tag_usm}\""
        buildConfigField "String", "tag_netinfo", "\"${tag_netinfo}\""
        buildConfigField "String", "tag_loc", "\"${tag_loc}\""
        buildConfigField "String", "tag_recerver", "\"${tag_recerver}\""
        buildConfigField "String", "tag_cutoff", "\"${tag_cutoff}\""
        buildConfigField "String", "tag_stringfog", "\"${tag_stringfog}\""


        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            /**打开覆盖率统计开关*/
            //testCoverageEnabled = true
            minifyEnabled false
            if (isPackageJar) {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-jar.pro'
            } else {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-dex.pro'
            }
        }
        release {
            minifyEnabled true
            if (isPackageJar) {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-jar.pro'
            } else {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-dex.pro'
            }
        }
    }

    dexOptions {
        preDexLibraries false
        maxProcessCount 8
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        warningsAsErrors false
        disable "UnusedResources"
        textOutput "stdout"
        textReport false
        disable 'RestrictedApi'
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'org.robolectric:robolectric:3.0'
    compileOnly files("libs/miit_mdid_1.0.10.aar")
}


task generateJar(type: Copy) {
    from("build/intermediates/bundles/release")
    into "../releasebak"
    include("classes.jar")
    def name = "analysys_track_v" + version.replace("|", "_") + (enableHotFix ? "00.jar" : ".jar")
    rename("classes.jar", name)
}

task copyDoc(type: Copy) {
    from("../doc")
    into "../releasebak"
    include("*.pdf")
}

//https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Zip.html
def zipPath = "../release/4.3.0.7_20200215/"
task packageZip(type: Zip) {
    from "../releasebak/"
//    destinationDir: 来指定生成zip文件的目录，类型是file(dir,但是不要包含/)
    destinationDir = file(zipPath)
//    archiveName: 压缩包名字，注意此处不会自动添加后缀
    archiveName = "analysys_track_v" + version.replace("|", "_") + (enableHotFix ? "00.zip" : ".zip")
}
task zip(type: Delete) {
    delete "../releasebak"
}
generateJar.dependsOn build
copyDoc.dependsOn generateJar
packageZip.dependsOn copyDoc
zip.dependsOn packageZip